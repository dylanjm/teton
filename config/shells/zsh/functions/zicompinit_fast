#!/usr/bin/env zsh
# Author: NICHOLAS85
# Source: https://github.com/NICHOLAS85/dotfiles/blob/xps_13_9365_refresh/.zinit/snippets/config/functions/zicompinit_fast
# https://gist.github.com/ctechols/ca1035271ad134841284

function zicompinit_fast() {
  setopt extended_glob local_options
  autoload -Uz compinit
  local zcompf="$ZINIT[ZCOMPDUMP_PATH]"

  ## Use a separate file to determine when to regenerate,
  ## as compinit doesn't always need to modify the compdump
  local zcompf_a="${zcompf}.augur"

  if [[ -e "$zcompf_a" && -f "$zcompf_a"(#qN.md-1) ]]; then
    compinit -C -d "$zcompf"
  else
    compinit -d "$zcompf"
    touch "$zcompf_a"
  fi

  # if zcompdump exists (and is non-zero), and is older than the .zwc file, then regenerate
  if [[ -s "$zcompf" && (! -s "${zcompf}.zwc" || "$zcompf" -nt "${zcompf}.zwc") ]]; then
    # since file is mapped, it might be mapped right now (current shells), so rename it then make a new one
    [[ -e "$zcompf.zwc" ]] && mv -f "$zcompf.zwc" "$zcompf.zwc.old"
    # compile it mapped, so multiple shells can share it (total mem reduction)
    # run in background
    { zcompile -M "$zcompf" && command rm -f "$zcompf.zwc.old" }&!
  fi
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
